import java.text.SimpleDateFormat

plugins {
    id "java-library"
    id "idea"
    id "eclipse"
    id 'maven'
    id 'signing'
    id 'net.nemerosa.versioning' version '2.8.2'
    id 'net.saliman.cobertura' version '2.5.4'
    id 'com.github.kt3k.coveralls' version '2.8.2'
}

group = 'io.github.joealisson'
version = '1.0.0-SNAPSHOT'

sourceCompatibility = JavaVersion.VERSION_1_8

ext.moduleName = 'io.github.joealisson.primitive'
ext.revision = versioning.info.build
ext.buildJDK = "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})".toString()

cobertura.coverageFormats = ['xml', 'html']

sourceSets {
    main {
        java {
            srcDirs = ['src/main/io.github.joealisson.primitive']
        }
        resources {
            srcDirs = ['src/main/resources']
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    testCompile group: 'org.testng', name: 'testng', version: '6.14.3'

}

compileJava {
    inputs.property("moduleName", moduleName)
    options.compilerArgs << "-Xlint:all"
}

jar {
    manifest {
        attributes('Built-By'       : System.getProperty('user.name'),
                'Build-Timestamp': new SimpleDateFormat("yyyy-MM-dd'T'HH:mm:ss.SSSZ").format(new Date()),
                'Build-Revision' : revision,
                'Build-Version'  : version,
                'Build-Jdk'      : buildJDK)
    }
}

task jarSources(type:Jar){
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

artifacts {
    archives javadocJar, jarSources, jar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'Primitive Collections'
                packaging 'jar'
                description 'Primitive collections framework based on Java Collections API. Forked from https://github.com/VISTALL/napile.primitive'
                url 'https://github.com/JoeAlisson/napile.primitive'

                licenses {
                    license {
                        name = 'GNU General Public License v3.0'
                        url = 'https://opensource.org/licenses/AGPL-3.0'
                    }
                }
                developers {
                    developer {
                        id = 'JoeAlisson'
                        name = 'Alisson Oliveira'
                        email = 'joe.alisson@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/JoeAlisson/napile.primitive.git'
                    developerConnection = 'scm:git:ssh://github.com:JoeAlisson/napile.primitive.git'
                    url = 'https://github.com/JoeAlisson/apile.primitive.git/tree/master'
                }
            }
        }
    }
}